plugins {
    id 'org.springframework.boot' version '2.7.2'
    id 'io.spring.dependency-management' version '1.0.12.RELEASE'
    id 'java'
    id "org.asciidoctor.jvm.convert" version "3.3.2"
    id 'checkstyle'
}

group = 'com'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
    asciidoctorExt
    compileOnly {
        extendsFrom annotationProcessor
    }
}

checkstyle {
    maxWarnings = 0 // 규칙이 어긋나는 코드가 하나라도 있을 경우 빌드 fail
    configFile = file("${rootDir}/config/google_checks.xml") // 설정 파일 경로 지정
    toolVersion = "10.3.2" // 처음에 낮은 버전을 사용했더니 깨지는 현상이 있었다. 설정파일의 내용에 맞는 버전을 선택
}

repositories {
    mavenCentral()
}

dependencies {
    // lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    // spring boot web
    implementation 'org.springframework.boot:spring-boot-starter-web'
    // spring boot devtools
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    // MongoDB spring boot starter
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    // Swagger 연동을 위한 spring doc open api
    implementation 'org.springdoc:springdoc-openapi-ui:1.6.9'
    // test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    // Rest Docs
    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
}

ext {
    snippetsDir = file('build/generated-snippets')
}

tasks.named('test') {
    outputs.dir snippetsDir
    useJUnitPlatform()
}

tasks.named('asciidoctor') {
    inputs.dir snippetsDir
    configurations 'asciidoctorExt'
    dependsOn test
}

tasks.named('bootJar') {
    dependsOn asciidoctor
    from("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}
